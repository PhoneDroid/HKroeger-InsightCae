!include FileFunc.nsh
!include LogicLib.nsh
!include nsDialogs.nsh
!include psexec.nsh
!include x64.nsh


Name "InsightCAE on Windows - Activate WSL"
OutFile "wsl-activation-installer.exe"
RequestExecutionLevel admin


Var insightInstallerPath

Function .onInit
    ${GetParameters} $0
    ${GetOptions} $0 "/insightInstallerPath=" $insightInstallerPath
    
    ${IfNot} ${RunningX64}
        MessageBox MB_OK "This is a 32bit system! Only 64bit systems are supported. The installer will stop now."
        abort
    ${EndIf} 
FunctionEnd



Page custom createWSLDlg "" "Activate WSL"

Page instfiles

Section "Activate WSL System"
    SetDetailsPrint both
    DetailPrint "Executing WSL install command..."
    
    ${DisableX64FSRedirection}
    ;${PowerShellExec} "dism.exe /online /enable-feature /featurename:VirtualMachinePlatform /all /norestart"
    ;${PowerShellExec} "dism.exe /online /enable-feature /featurename:Microsoft-Windows-Subsystem-Linux /all /norestart"
    ;${PowerShellExec} "wsl --install --no-distribution" ; --no-distribution seems to prevent a complete install
    ${PowerShellExec} "wsl --install"
    WriteRegStr HKCU "Software\Microsoft\Windows\Currentversion\Runonce" "InstallResume" '"$insightInstallerPath"'
SectionEnd


Page custom createWSLFinishedDlg doReboot "Reboot" /ENABLECANCEL

Var hWnd

Function createWSLDlg
    nsDialogs::Create 1018
    Pop $0

    ${NSD_CreateLabel} 0 0 100% 100% "Please click $\"Install$\" to activate the Windows Subsystem for Linux (WSL) now!"
    Pop $hwnd
    ${NSD_AddStyle} $hwnd ${SS_CENTER}

    nsDialogs::Show
FunctionEnd



Function createWSLFinishedDlg
    nsDialogs::Create 1018
    Pop $0

    ${NSD_CreateLabel} 0 0 100% 100% "The activation is done.$\r$\nThe system has to be rebooted to apply the changes!$\r$\nPlease click on $\"Close$\" to reboot now or $\"Cancel$\" to skip restarting.$\r$\nThe InsightCAE installer will automatically resume after the next reboot."
    Pop $hwnd
    ${NSD_AddStyle} $hwnd ${SS_CENTER}

    nsDialogs::Show
FunctionEnd

Function doReboot
    Reboot
FunctionEnd

